-module({{packageName}}_jesse_validator_swagger_2_0).

-behaviour(jesse_schema_validator).

%% API
-export([init_state/0]).
-export([check_value/3]).
-export([options/0]).

-define(DISCRIMINATOR, <<"discriminator">>).
-define(REF,           <<"$ref">>).
-define(NOT_FOUND,     not_found).

-spec options() ->
    [{validator, module()} | {validator_state, []}].
options() ->
    [
        {validator, ?MODULE}
    ].

-spec init_state() ->
    [].
init_state() ->
    [].

-spec check_value(Value, Attr, State) ->
    State | no_return()
when
    Value :: any(),
    Attr  :: {binary(), jesse:json_term()},
    State :: jesse_state:state().
check_value(Value, {?DISCRIMINATOR, DiscrField}, State) ->
    case jesse_lib:is_json_object(Value) of
        true  -> validate_discriminator(Value, DiscrField, State);
        false -> State
    end;
check_value(Value, Attr = {?REF, Ref}, State) ->
    case is_recursive_ref(Ref, State) of
        true -> State;
        false -> validate_ref(Value, Attr, State)
    end;
check_value(Value, Attr, State) ->
    jesse_validator_draft4:check_value(Value, Attr, State).

%%

validate_discriminator(Value, DiscrField, State) when is_binary(DiscrField) ->
    case jesse_json_path:value(DiscrField, Value, ?NOT_FOUND) of
        ?NOT_FOUND ->
            State;
        SchemaName ->
            validate_child_schema(Value, SchemaName, State)
    end.

validate_child_schema(Value, SchemaName, State) ->
    Schema = make_ref_schema(<<"#/definitions/", SchemaName/binary>>),
    jesse_schema_validator:validate_with_state(Schema, Value, State).

validate_ref(Value, Attr = {_, Ref}, State) ->
    State1 = add_ref_to_state(State, Ref),
    State2 = jesse_validator_draft4:check_value(Value, Attr, State1),
    remove_last_ref_from_state(State2).

add_ref_to_state(State, Ref) ->
    Path = jesse_state:get_validator_state(State),
    jesse_state:set_validator_state(State, [Ref | Path]).

remove_last_ref_from_state(State) ->
    case jesse_state:get_validator_state(State) of
        [_ | Rest] ->
            jesse_state:set_validator_state(State, Rest);
        [] ->
            State
    end.

is_recursive_ref(Ref, State) ->
    lists:member(Ref, jesse_state:get_validator_state(State)).

make_ref_schema(Ref) ->
    [{?REF, Ref}].


-ifdef(TEST).
-include_lib("eunit/include/eunit.hrl").

-define(SCHEMA,
  <<"{\"definitions\": {
       \"Pet\": {
         \"type\":          \"object\",
         \"discriminator\": \"type\",
         \"properties\": {
            \"name\":    {\"type\": \"string\"},
            \"owner\":   {\"$ref\": \"#/definitions/Owner\"},
            \"petType\": {\"type\": \"string\"}
         },
         \"required\": [\"name\", \"type\"]
       },
       \"Cat\": {
         \"description\": \"A representation of a cat\",
         \"allOf\": [
           {\"$ref\": \"#/definitions/Pet\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"huntingSkill\": {
                 \"type\":        \"string\",
                 \"description\": \"The measured skill for hunting\",
                 \"default\":     \"lazy\",
                 \"enum\":        [\"clueless\", \"lazy\", \"adventurous\", \"aggressive\"]
               }
             },
             \"required\": [\"huntingSkill\"]
           }
         ]
       },
       \"Dog\": {
         \"description\": \"A representation of a dog\",
         \"allOf\": [
           {\"$ref\": \"#/definitions/Pet\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"packSize\": {
                 \"type\":        \"integer\",
                 \"format\":      \"int32\",
                 \"description\": \"the size of the pack the dog is from\",
                 \"default\":     0,
                 \"minimum\":     0
               }
             }
           }
         ],
         \"required\": [\"packSize\"]
       },
       \"Owner\": {
         \"type\":          \"object\",
         \"discriminator\": \"type\",
         \"properties\": {
            \"soleOwner\": {\"type\": \"boolean\"},
            \"type\": {\"type\": \"string\"}
         },
         \"required\": [\"soleOwner\", \"type\"]
       },
       \"Family\": {
         \"allOf\": [
           {\"$ref\": \"#/definitions/Owner\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"members\": {
                 \"type\":  \"array\",
                 \"items\": {\"$ref\": \"#/definitions/Person\"},
                 \"minItems\": 1
               }
             },
             \"required\": [\"members\"]
           }
         ]
       },
       \"Person\": {
         \"allOf\": [
           {\"$ref\": \"#/definitions/Owner\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"name\": {\"type\": \"string\"},
               \"age\":  {
                 \"type\":    \"integer\",
                 \"minimum\": 0,
                 \"maximum\": 150
               },
               \"sex\": {
                 \"type\": \"string\",
                 \"enum\": [\"male\", \"female\"]
               },
               \"legalStatus\": {\"$ref\": \"#/definitions/LegalStatus\"}
             },
             \"required\": [\"age\", \"sex\"]
           }
         ]
       },
       \"LegalStatus\": {
         \"type\":          \"object\",
         \"discriminator\": \"status\",
         \"properties\": {
            \"status\": {\"type\": \"string\"}
         },
         \"required\": [\"status\"]
       },
       \"Single\": {
         \"allOf\": [
           {\"$ref\": \"#/definitions/LegalStatus\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"minor\": {\"type\": \"boolean\"}
             },
             \"required\": [\"minor\"]
           }
         ]
       },
       \"Married\": {
         \"allOf\": [
           {\"$ref\": \"#/definitions/LegalStatus\"},
           {
             \"type\":       \"object\",
             \"properties\": {
               \"partnerName\": {\"type\": \"string\"}
             },
             \"required\": [\"partnerName\"]
           }
         ]
       }
     }}">>).
-define(PET_SCHEMA, "Pet").

init_jesse() ->
    jesse:add_schema(?SCHEMA_KEY, ?SCHEMA, [{parser_fun, fun jsx:decode/1}]).

validate(DefName, Value) ->
    Schema = jsx:decode(?SCHEMA, [return_maps]),
    case jesse:validate_definitions(DefName, Schema, Value, options()) of
        {ok, Value}    -> Value;
        {error, Error} -> Error
    end.

exception(BadSchema, BadData, PropertyPath) ->
    [{
        data_invalid,
        make_ref_schema(BadSchema),
        all_schemas_not_valid,
        BadData,
        PropertyPath
     }].

%% Test cases
-spec test() -> _.

-spec success_test() -> _.
success_test() ->
    Pet = jsx:decode(<<"{
        \"type\":         \"Cat\",
        \"name\":         \"Flaffy\",
        \"huntingSkill\": \"adventurous\",
        \"owner\": {
            \"type\":      \"Person\",
            \"soleOwner\": true,
            \"name\":      \"Robin\",
            \"age\":       12,
            \"sex\":       \"male\",
            \"legalStatus\": {
                \"status\": \"Single\",
                \"minor\":  true
            }
        }
    }">>),
    ?assertEqual(Pet, validate(?PET_SCHEMA, Pet)).

-spec wrong_cat_test() -> _.
wrong_cat_test() ->
    Pet = jsx:decode(<<"{
        \"name\": \"Fluffy\",
        \"type\": \"Cat\",
        \"huntingSkill\": \"wrong\"
    }">>),
    Expect = exception(<<"#/definitions/Pet">>, Pet, []),
    ?assertEqual(Expect, validate(?PET_SCHEMA, Pet)).

-spec person_sex_missing_test() -> _.
person_sex_missing_test() ->
    Pet = jsx:decode(<<"{
        \"type\":         \"Cat\",
        \"name\":         \"Fluffy\",
        \"huntingSkill\": \"adventurous\",
        \"owner\": {
            \"type\":      \"Person\",
            \"soleOwner\": true,
            \"name\":      \"true\",
            \"age\":       12,
            \"legalStatus\": {
                \"status\": \"Single\",
                \"minor\":  true
            }
        }
    }">>),
    Expect = exception(<<"#/definitions/Owner">>, jesse_json_path:path(<<"owner">>, Pet), [<<"owner">>]),
    ?assertEqual(Expect, validate(?PET_SCHEMA, Pet)).

-spec legal_status_minor_flag_missing_test() -> _.
legal_status_minor_flag_missing_test() ->
    Pet = jsx:decode(<<"{
        \"type\":         \"Cat\",
        \"name\":         \"Fluffy\",
        \"huntingSkill\": \"adventurous\",
        \"owner\": {
            \"type\":      \"Person\",
            \"soleOwner\": true,
            \"name\":      \"true\",
            \"age\":       12,
            \"sex\":       \"male\",
            \"legalStatus\": {
                \"status\": \"Single\"
            }
        }
    }">>),
    Expect = exception(<<"#/definitions/Owner">>, jesse_json_path:path(<<"owner">>, Pet), [<<"owner">>]),
    ?assertEqual(Expect, validate(?PET_SCHEMA, Pet)).

-endif.
